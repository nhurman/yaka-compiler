/*
 * Auteur: Pedrono Marie-Jo
 * création : 13 février 2002
 * nature : projet etudiant
 */

options{
  JDK_VERSION="1.5";
  STATIC=false;
}

PARSER_BEGIN(Yaka)

package YakaC.javacc;

import YakaC.*;
import YakaC.Parser.Context;
import YakaC.Parser.Expression;
import YakaC.Parser.ErrorBag;
import YakaC.Parser.Ident;
import YakaC.Exception.YakaException;

public class Yaka
{
  protected Context m_context; /**< Yaka context */

  /** Events */
  public static enum Event implements YakaC.Event.Event
  {
    ProgramStart,
    ProgramEnd,
    InstructionsStart,
    Operation,
    FunctionDeclaration,
    FunctionCall,
    FunctionEnd,
    Return,

    Integer,
    Identifier,
    Boolean;
  }

  /**
   * Second constructor.
   * Call this function right after 'new Yaka()'
   */
  public void init()
  {
    m_context = new Context(this);
  }

  /**
   * Fetch the errors list
   * @return ErrorBag
   */
  public ErrorBag errors()
  {
    return m_context.errorBag();
  }

  /**
   * Fetch the context
   * @return Context
   */
  public Context context()
  {
    return m_context;
  }
}

PARSER_END(Yaka)

/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS:
{
  public String identLu, chaineLue;
  public int entierLu;
}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);}
}


/* Mots réservés*/
TOKEN :
{
  <BOOLEEN: "BOOLEEN">
| <VAR: "VAR">
| <FAIRE: "FAIRE">
| <SINON: "SINON">
| <POUR: "POUR">
| <SI: "SI">
| <FSI: "FSI">
| <ENTIER: "ENTIER">
| <RETOURNE: "RETOURNE">
| <VRAI: "VRAI">
| <TANTQUE: "TANTQUE">
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ >
  { entierLu = Integer.parseInt(image.toString()); }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
  { identLu = image.toString(); }
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
  { chaineLue = image.toString(); }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() throws YakaException : {}
{
  <PROGRAMME> <ident> { m_context.eventManager().emit(Event.ProgramStart, token_source.identLu); }
  (declFonction())*
  <PRINCIPAL>
  {
    m_context.declaration().function(null);
    m_context.eventManager().emit(Event.FunctionDeclaration, "main");
  }
  bloc()
  <FPRINCIPAL>
  <FPROGRAMME> { m_context.eventManager().emit(Event.ProgramEnd); }
}

void declFonction() throws YakaException : {}
{
  type() <FONCTION> <ident>
  { m_context.declaration().function(token_source.identLu); }
  paramForms()
  {
    m_context.declaration().function();
    m_context.eventManager().emit(Event.FunctionDeclaration, m_context.declaration().name());
  }
  bloc()
  <FFONCTION>
  { m_context.eventManager().emit(Event.FunctionEnd); }
}

void paramForms() throws YakaException : {}
{
  "(" (paramForm() ("," paramForm())*)? ")"
}

void paramForm() throws YakaException : {}
{
  type() <ident>
  { m_context.declaration().parameter(token_source.identLu); }
}

void bloc() throws YakaException : {}
{
 (declConst())*
 (declVar())*
 { m_context.eventManager().emit(Event.InstructionsStart); }
   suiteInstr()
}

void declConst() throws YakaException : {}
{
  <CONST> defConst() ( "," defConst())* ";"
}

void defConst() throws YakaException : {}
{
  <ident> { m_context.declaration().constant(token_source.identLu); }

  "=" valConst()
}

void valConst() throws YakaException : {}
{
  <entier> { m_context.declaration().constant(Ident.Type.Integer, token_source.entierLu); }
 | <ident> { m_context.declaration().constant(m_context.locals().find(token_source.identLu)); }
 | <VRAI>  { m_context.declaration().constant(Ident.Type.Boolean, true); }
 | <FAUX>  { m_context.declaration().constant(Ident.Type.Boolean, false); }
}


void declVar() throws YakaException : {}
{
  <VAR> type()
  <ident> { m_context.declaration().variable(token_source.identLu); }
  (","
      <ident> { m_context.declaration().variable(token_source.identLu); }
  )* ";"

}

void type() : {}
{
   <ENTIER>  { m_context.declaration().type(Ident.Type.Integer); }
 | <BOOLEEN> { m_context.declaration().type(Ident.Type.Boolean); }
}

/*
 * Syntaxe des instructions.
 */
void suiteInstr() throws YakaException : {}
{ (instruction() (";" (instruction())? )*)? }


void instruction() throws YakaException : {}
{
  affectation()
| lecture()
| ecriture()
| iteration()
| condition()
| retourne()
}

void affectation() throws YakaException : {}
{
  <ident> { m_context.affectation().assign(token_source.identLu); }
  "="
  expression() { m_context.affectation().assign(); }
}

void lecture() throws YakaException : {}
{
  <LIRE> "(" <ident> ")"
  { m_context.io().read(token_source.identLu); }
}

void ecriture() throws YakaException : {}
{
  <ECRIRE> "("
  (
    expression() { m_context.io().write(); }
  | <chaine>     { m_context.io().write(token_source.chaineLue); }
  ) ")"
| <ALALIGNE>     { m_context.io().newLine(); }
}

void iteration() throws YakaException : {}
{
  <TANTQUE>      { m_context.iteration().beginFor(); }
    expression() { m_context.iteration().condition(); }
  <FAIRE>
    suiteInstr()
  <FAIT>         { m_context.iteration().endFor(); }
}

void condition() throws YakaException : {}
{
  <SI>           { m_context.branching().beginIf(); }
    expression() { m_context.branching().condition(); }
  <ALORS>
    suiteInstr()
 (<SINON>        { m_context.branching().beginElse(); }
    suiteInstr()
 )?
  <FSI>          { m_context.branching().endIf(); }
}

void retourne() throws YakaException: {}
{
  <RETOURNE> expression()
  {
    m_context.eventManager().emit(Event.Return);
    m_context.expression().returnValue();
  }
}

/*
 * Expression.
 */
void expression() throws YakaException : {}
{
  simpleExpr()
  (
    opRel()
    simpleExpr() { m_context.expression().operation(); }
  )?
}

void simpleExpr() throws YakaException : {}
{
   terme()
   (
    opAdd()
    terme() { m_context.expression().operation(); }
   )*
}

void terme() throws YakaException : {}
{
  facteur()
  (
    opMul()
    facteur() { m_context.expression().operation(); }
  )*
}

void facteur() throws YakaException : {}
{
   primaire()
 | opNeg() primaire() { m_context.expression().operation(); }
}

void primaire() throws YakaException: {}
{
   valeur()
 | "(" expression() ")"

}

void valeur() throws YakaException : {}
{
  <entier>
  {
    m_context.expression().push(Ident.Type.Integer);
    m_context.eventManager().emit(Event.Integer, new Integer(token_source.entierLu));
  }
| ident()
| <VRAI>
  {
    m_context.expression().push(Ident.Type.Boolean);
    m_context.eventManager().emit(Event.Boolean, new Boolean(true));
  }
| <FAUX>
  {
    m_context.expression().push(Ident.Type.Boolean);
    m_context.eventManager().emit(Event.Boolean, new Boolean(false));
  }
}

void ident() throws YakaException :
{
  String identLu;
}
{
  <ident> { identLu = token_source.identLu; }
  (
    (
      "(" { m_context.expression().call(m_context.globals().find(identLu)); }
      (argumentsFonction())?
      ")"
      {
        m_context.expression().call();
        m_context.eventManager().emit(Event.FunctionCall, identLu);
      }
    )
    |
    {
      m_context.expression().push(m_context.locals().find(identLu));
      m_context.eventManager().emit(Event.Identifier, identLu);
    }
  )
}

void argumentsFonction() throws YakaException : {}
{
  argumentFonction()
  ("," argumentFonction())*
}

void argumentFonction() throws YakaException : {}
{
  expression() { m_context.expression().functionParameter(); }
}

void opRel() : {}
{
   "="  { m_context.expression().push(Expression.Operator.Equals); }
 | "<>" { m_context.expression().push(Expression.Operator.NEquals); }
 | "<"  { m_context.expression().push(Expression.Operator.Lower); }
 | "<=" { m_context.expression().push(Expression.Operator.LowerE); }
 | ">"  { m_context.expression().push(Expression.Operator.Greater); }
 | ">=" { m_context.expression().push(Expression.Operator.GreaterE); }
}

void opAdd() : {}
{
    "+" { m_context.expression().push(Expression.Operator.Plus); }
  | "-" { m_context.expression().push(Expression.Operator.Minus); }
  | <OU> { m_context.expression().push(Expression.Operator.Or); }
}

void opMul() : {}
{
    "*" { m_context.expression().push(Expression.Operator.Times); }
  | "/" { m_context.expression().push(Expression.Operator.Div); }
  | <ET> { m_context.expression().push(Expression.Operator.And); }
}

void opNeg() : {}
{
    "-" { m_context.expression().push(Expression.Operator.Negate); }
  | <NON> { m_context.expression().push(Expression.Operator.Not); }
}


