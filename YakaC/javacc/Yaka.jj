/*
 * Auteur: Pedrono Marie-Jo
 * création : 13 février 2002
 * nature : projet etudiant
 */

options{
	JDK_VERSION="1.5";
}

PARSER_BEGIN(Yaka)

package YakaC.javacc;

import YakaC.*;
import YakaC.Event.*;
import YakaC.Exception.YakaException;

public class Yaka {
  public static EventManager eventManager;
  public static ErrorBag errors;
  public static TabIdent tabIdent;
  public static Declaration declaration;
  public static Expression expression;
  public static YVM yvm;

  public static void init(java.io.OutputStream os)
  {
    eventManager = new EventManager();
    errors = new ErrorBag(eventManager, false);
    tabIdent = new TabIdent(errors, 10);
    declaration = new Declaration(errors, eventManager, tabIdent);
    expression = new Expression(errors, eventManager);
    yvm = new YVM(os, eventManager, tabIdent);
  }

  public static void main(String args[])
  {
    Yaka analyseur;
    java.io.InputStream input;
    String yvmFilename;

    if (args.length == 2) {
      try {
        input = new java.io.FileInputStream(args[0]);
        yvmFilename = args[1];
      } catch (java.io.FileNotFoundException e) {
        System.err.println("Fichier introuvable.");
        return;
      }
    } else if (args.length == 1) {
      System.err.println("Lecture sur l'entree standard...");
      input = System.in;
      yvmFilename = args[0];
    } else {
      System.err.println("Usage: java " + Yaka.class.getName() + " [yaka] [yvm]");
      return;
    }
    try {
      java.io.File fp = new java.io.File(yvmFilename);
      java.io.FileOutputStream os = new java.io.FileOutputStream(fp);
      init(System.out);

      analyseur = new Yaka(input);
      analyseur.analyse();
      //System.out.println(declaration + "\n");
      //System.out.println(expression);
    } catch (java.io.FileNotFoundException e) {
      System.err.println("Impossible d'ouvrir le fichier de sortie.");
    } catch (ParseException e) {
      errors.add(new YakaC.Exception.ParseException(e.getMessage()));
      //msg = msg.substring(0,msg.indexOf("\n"));
    } catch (YakaException e) {
      System.err.println("Erreur de compilation : " + e.toString());
    } finally {
      System.err.println(errors);
    }
  }
}

PARSER_END(Yaka)

/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS:
{
  public static String identLu, chaineLue;
  public static int entierLu;
}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);}
}


/* Mots réservés*/
TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ >
	{ entierLu = Integer.parseInt(image.toString()); }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{ identLu = image.toString();	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString(); }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() throws YakaException : {}
{
  <PROGRAMME> <ident> { eventManager.emit(Event.ProgramStart, YakaTokenManager.identLu); }
  bloc()
  <FPROGRAMME> { eventManager.emit(Event.ProgramEnd); }
}

void bloc() throws YakaException : {}{
 (declConst())*
 (declVar())*
 { eventManager.emit(Event.ExpressionsStart); }
   suiteExpr()
}

void declConst() throws YakaException : {}
{
  <CONST> defConst() ( "," defConst())* ";"
}

void defConst() throws YakaException : {}
{
  <ident> { declaration.constant(YakaTokenManager.identLu); }

  "=" valConst()
}

void valConst() throws YakaException : {}{
  <entier> { declaration.constant(Ident.Type.Integer, YakaTokenManager.entierLu); }
 | <ident> { declaration.constant(tabIdent.find(YakaTokenManager.identLu)); }
 | <VRAI>  { declaration.constant(Ident.Type.Boolean, true); }
 | <FAUX>  { declaration.constant(Ident.Type.Boolean, false); }
}


void declVar() throws YakaException : {}
{
  <VAR> type()
  <ident> { declaration.variable(YakaTokenManager.identLu); }
  (","
      <ident> { declaration.variable(YakaTokenManager.identLu); }
  )* ";"

}

void type() : {}
{
   <ENTIER>  { declaration.variable(Ident.Type.Integer); }
 | <BOOLEEN> { declaration.variable(Ident.Type.Boolean); }
}

/*
 * Syntaxe des instructions.
 */
void suiteExpr() throws YakaException : {}
{ (expression() (";" (expression())? )*)? }


/*
 * Expression.
 */
void expression() throws YakaException : {}
{
  simpleExpr()
  (
    opRel()
    simpleExpr() { expression.operation(); }
  )?
}

void simpleExpr() throws YakaException : {}
{
   terme()
   (
    opAdd()
    terme() { expression.operation(); }
   )*
}

void terme() throws YakaException : {}
{
  facteur()
  (
    opMul()
    facteur() { expression.operation(); }
  )*
}

void facteur() throws YakaException : {}
{
   primaire()
 | opNeg() primaire() { expression.operation(); }
}

void primaire () throws YakaException: {}
{
   valeur()
 | "(" expression() ")"

}

void valeur () throws YakaException : {}
{
   <entier> {
    expression.push(Ident.Type.Integer);
    eventManager.emit(Event.Integer, new Integer(YakaTokenManager.entierLu));
   }
 | <ident> {
    expression.push(tabIdent.find(YakaTokenManager.identLu));
    eventManager.emit(Event.Identifier, YakaTokenManager.identLu);
  }
 | <VRAI> {
    expression.push(Ident.Type.Boolean);
    eventManager.emit(Event.Boolean, new Boolean(true));
  }
 | <FAUX> {
    expression.push(Ident.Type.Boolean);
    eventManager.emit(Event.Boolean, new Boolean(false));
 }
}

void opRel() : {}
{
   "="  { expression.push(Expression.Operator.Equals); }
 | "<>" { expression.push(Expression.Operator.NEquals); }
 | "<"  { expression.push(Expression.Operator.Lower); }
 | "<=" { expression.push(Expression.Operator.LowerE); }
 | ">"  { expression.push(Expression.Operator.Greater); }
 | ">=" { expression.push(Expression.Operator.GreaterE); }
}

void opAdd() : {}
{
    "+" { expression.push(Expression.Operator.Plus); }
  | "-" { expression.push(Expression.Operator.Minus); }
  | <OU> { expression.push(Expression.Operator.Or); }
}

void opMul() : {}
{
    "*" { expression.push(Expression.Operator.Times); }
  | "/" { expression.push(Expression.Operator.Div); }
  | <ET> { expression.push(Expression.Operator.And); }
}

void opNeg() : {}
{
    "-" { expression.push(Expression.Operator.Negate); }
  | <NON> { expression.push(Expression.Operator.Negate); }
}


